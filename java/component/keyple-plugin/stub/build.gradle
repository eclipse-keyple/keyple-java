project.version = '0.7.0'+'-'+ timestamp+ '-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'java-library'

sourceCompatibility = 1.6

project.description ='Keyple Plugin Stub'

//copy license file from root folder to each generated artifact
task copyLICENSEplugin(type: Copy) {
    from file("${projectDir}/../../../../LICENSE")
    into file("${buildDir}/resources/main/META-INF/")
}

//copy notice file from root folder to each generated artifact
task copyNOTICEplugin(type: Copy) {
    from file("${projectDir}/../../../../NOTICE.md")
    into file("${buildDir}/resources/main/META-INF/")
}


classes.dependsOn copyLICENSEplugin, copyNOTICEplugin

//declare manifest
jar {
    manifest {
        attributes 'Implementation-Title': project.description,
                'Implementation-Version': project.version
    }
}

//generate sources jar
task sourcesJar(type: Jar, dependsOn : classes) {
    classifier = 'sources'
    from sourceSets.main.output, sourceSets.main.java
}

//generate javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//publish javadoc, sources and jar
artifacts {
    archives sourcesJar, javadocJar
}

/*
Required to share unit tests
 */
configurations {
    testArtifacts.extendsFrom testRuntime
}

task testJar(type: Jar) {
    classifier "test"
    from sourceSets.test.output
}

artifacts {
    testArtifacts testJar
}


dependencies {

    //use java library : core
    api project(path:':java:component:keyple-core')

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    api "org.slf4j:slf4j-api:${slf4japi_version}"


    /*
    Test
     */

    //use java library :  calypso
    testImplementation  project(path:':java:component:keyple-calypso')

    // https://mvnrepository.com/artifact/junit/junit
    testImplementation "junit:junit:${junit_version}"

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation "org.mockito:mockito-core:${mockitocore_version}"

    //add slf4j simple logger implementation
    testImplementation "org.slf4j:slf4j-simple:${slf4jsimple_version}"

}

test {
    include 'org/**'
}
